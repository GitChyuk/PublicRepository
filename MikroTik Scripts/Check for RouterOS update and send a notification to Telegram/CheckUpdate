# Script: Check for RouterOS update and send a notification to Telegram
# Version: 1.00
# Yun Sergey (c) 2020
# Specify your variable values: TelegramBotToken, TelegramChatID
# Script policy: read
# URL: https://mhelp.pro/mikrotik-scripts-check-routeros-update/

# Function: Send a message to Telegram
:local TGSendMessage do={
    :local tgUrl "https://api.telegram.org/bot$Token/sendMessage?chat_id=$ChatID&text=$Text&parse_mode=html&disable_web_page_preview=True";
    /tool fetch http-method=get url=$tgUrl keep-result=no;
}

# Variables
:local TelegramBotToken "987654321:AAFJIVTAWodBwzGX2CLne6-PK4RFNSy-8OY";
:local TelegramChatID "987654321";
:local DeviceName [/system identity get name];
:local Channel [/system package update get channel];
:local InstVer [/system package update get installed-version];
:local LatVer [/system package update get latest-version];
:local MessageText "\F0\9F\9F\A2 <b> $DeviceName:</b>  ";

# Main script code
:if ($InstVer = $LatVer) do={
    :set MessageText  ($MessageText . "System is already up to date");
} else={
    :set MessageText  "$MessageText New version $LatVer is available! <a href=\"https://mikrotik.com/download/changelogs\">Changelogs</a>. [Installed version $InstVer, chanell $Channel].";
    $TGSendMessage Token=$TelegramBotToken ChatID=$TelegramChatID Text=$MessageText;
}
:log info $MessageText;
